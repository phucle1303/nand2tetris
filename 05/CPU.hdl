// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    // Mux16 selects between A instruction and C instruction
    Mux16(a=instruction, b=ALURegIn, sel=instruction[15], out=Mux16Out1);

    // Put instruction to A register
    Not(in=instruction[15], out=NotInstr15);
	Or(a=NotInstr15, b=instruction[5], out=loadA);
    ARegister(in=Mux16Out1, load=loadA, out=ARegOut, out[0..14]=addressM);

    // Mux16 selects between A instruction and inM
    Mux16(a=ARegOut, b=inM, sel=instruction[12], out=Mux16Out2);

    // D register
    And(a=instruction[4], b=instruction[15], out=loadD);
    DRegister(in=ALURegIn, load=loadD, out=DRegOut);

    // writeM
    And(a=instruction[15], b=instruction[3], out=writeM);

    // ALU
    ALU(x=DRegOut, y=Mux16Out2, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=ALURegIn, zr=ALUzr, ng=ALUng);

    // PC
    Mux16(a=false, b[0..2]=instruction[0..2], b[3..15]=false, sel=instruction[15], out[0]=jmpIn0, out[1]=jmpIn1, out[2]=jmpIn2);
    Not(in=ALUzr, out=ALUzrNot);
    Not(in=ALUng, out=ALUngNot);
    And(a=ALUzrNot, b=ALUngNot, out=pos);
    And(a=jmpIn0, b=pos, out=JGT);
    And(a=jmpIn1, b=ALUzr, out=JEQ);
    And(a=jmpIn2, b=ALUng, out=JLT);
    Or(a=JGT, b=JEQ, out=JGE);
    Or(a=JGE, b=JLT, out=jump);
    PC(in=ARegOut, load=jump, inc=true, reset=reset, out[0..14]=pc);
}